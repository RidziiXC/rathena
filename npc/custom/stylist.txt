//===== rAthena Script =======================================
//= Stylist
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Changes your hair style, hair color, and cloth color.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Switched to 'getbattleflag', credits to Saithis. [Euphy]
//============================================================

prontera,170,180,1	script	Stylist#custom_stylist	122,{
	.@isSummoner = (eaclass() == EAJ_SUMMONER);
	setarray .@maxStyles[1],getbattleflag("max_cloth_color"),getbattleflag("max_hair_style"),getbattleflag("max_hair_color");
	setarray .@looks[1],LOOK_CLOTHES_COLOR,LOOK_HAIR,LOOK_HAIR_COLOR;
	.@s = select(" ~ สีเสื้อ: ~ ทรงผม: ~ สีผม: ~ ชุด Class 3");
	if(.@s == 4){
		.@type = (select("แบบที่ 1:แบบที่ 2") - 1);
		atcommand "@bodystyle " + .@type;
		end;
	}
	if((.@s == 2) && .@isSummoner)
	.@maxStyles[2] = 10;
	.@revert = getlook(.@looks[.@s]);
	.@selected = (((.@s == 2) && .@isSummoner) ? 0 : 1);
	while(1) {
		setlook .@looks[.@s],.@selected;
		message strcharinfo(0),"แบบที่ #" + .@selected;
		.@menu$ = " ~ ต่อไป: ~ ก่อนหน้า: ~ ข้ามไปที่...: ~ กลับสู่แบบเดิม (^0055FF" + .@revert + "^000000)";
		switch(select(.@menu$)){
		case 1:
			if(.@selected >= .@maxStyles[.@s]){
				.@min = (((.@s == 2) && .@isSummoner) ? 0 : 1);
				.@selected = .@min;
			}
			else
			.@selected++;
			break;
		case 2:
			.@min = .@isSummoner ? 0 : 1;
			if(.@selected <= .@min)
			.@selected = .@maxStyles[.@s];
			else
			.@selected--;
			break;
		case 3:
			message strcharinfo(0),"เลือกแบบที่ 1~" + .@maxStyles[.@s];
			input .@selected,0,.@maxStyles[.@s];
			if (!.@selected)
			.@selected = rand(1,.@maxStyles[.@s]);
			break;
		case 4:
			.@selected = .@revert;
			setlook .@looks[.@s],.@revert;
			break;
		}
	}
}
